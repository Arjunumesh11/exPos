//device manager
//module 4

//R1 function number
//R2 PID

alias functionNum R1;
alias currentPID R2;

//terminal write 3
//R3 word
if (functionNum == 3) then
    alias word R3;
    multipush(R1,R2,R3);
    
    // acquire terminal
    functionNum = 8;
    call RESOURCE_MANAGER;
    multipop(R1,R2,R3);
    
    // write 
    print word;
    //frees terminal
    multipush(R1,R2,R3);
    functionNum = 9;
    call RESOURCE_MANAGER;
    multipop(R1,R2,R3);
    return;
endif;

//terminal read 4
//R3 word
if (functionNum == 4) then
    alias word R3;
    multipush(R1,R2,R3);
    
    // acquire terminal
    functionNum = 8;
    call RESOURCE_MANAGER;
    multipop(R1,R2,R3);
    
    // read 
    //print "read_start";
    read;
    [PROCESS_TABLE + 16*currentPID + 4] = WAIT_TERMINAL;
    multipush(R1,R2,R3);
    call SCHEDULER;
   // print "read_end";
    multipop(R1,R2,R3);
    alias word_address R4;
    word_address = [PTBR + 2 * (word/ 512)] * 512 + (word % 512);
    [word_address] = [PROCESS_TABLE + currentPID*16 + 8];
    return;
endif;
    